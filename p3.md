# Como inicializar uma matriz de vetores
(ou qualquer array de arrays, lista de listas)

Há diferentes formas de incializar uma matriz que contenha vetores em cada posição. Algumas destas formas são:

## 1. Usar uma compreensão ([*comprehension*](https://docs.julialang.org/en/v1/manual/arrays/#man-comprehensions)) 

Esta forma é a mais compacta útil, já que os elementos da matriz já começam incializados como vetores:

```julia
julia> m = [ zeros(0) for i in 1:2, j in 1:2 ]
2×2 Array{Array{Float64,1},2}:
 []  []
 []  []

julia> push!(m[1,1],1)
1-element Array{Float64,1}:
 1.0

julia> m
2×2 Array{Array{Float64,1},2}:
 [1.0]  []
 []     []
```


## 2. Criar uma matriz com valores indeterminados, usando uma notação explicita:

```julia
julia> m = Matrix{Vector{Float64}}(undef,3,3)
3×3 Array{Array{Float64,1},2}:
 #undef  #undef  #undef
 #undef  #undef  #undef
 #undef  #undef  #undef

```
Neste caso, a matriz não tem nada nas posições ainda, portanto o primeiro acesso a um de seus elementos precisa
inicializar o vetor na posição correspondente. Algumas possibilidades são:

```julia

julia> m[1,1] = []
Any[]

julia> m[1,2] = [17]
1-element Array{Int64,1}:
 17

julia> m[1,3] = zeros(3)
3-element Array{Float64,1}:
 0.0
 0.0
 0.0

julia> m
3×3 Array{Array{Float64,1},2}:
    []      [17.0]     [0.0, 0.0, 0.0]
 #undef  #undef     #undef
 #undef  #undef     #undef

```

Se você tentar colocar qualquer coisa que não seja um vetor de números Float64 em uma posição, vai obter um erro:

```julia
julia> m[2,2] = 19
ERROR: MethodError: Cannot `convert` an object of type Int64 to an object of type Array{Float64,1}

```

Para obter o mesmo resultado final da primeira opção, é necessário preencher a matriz em um loop explícito:

```julia
julia> for i in 1:3
         for j in 1:3
           m[i,j] = []
         end
       end

julia> m
3×3 Array{Array{Float64,1},2}:
 []  []  []
 []  []  []
 []  []  []


```



